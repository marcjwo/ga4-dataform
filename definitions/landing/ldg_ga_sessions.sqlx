config {
    type: "incremental",
    schema: dataform.projectConfig.vars.LDG_DATA,
    description: "table containing records of Google Analytics sessions from GA4",
    bigquery: {
        partitionBy: "session_date"
    },
    assertions: {
        uniqueKey: ["session_id"]
    },
    tags: ["landing"]
}

SELECT
  -- deduplicate session-scoped data ...
  DISTINCT
  -- window functions will produce multiple equivalent records
  session_id,
  client_id,
  -- apply window calculations
  LAST_VALUE(continent) OVER(session_window) AS continent,
  LAST_VALUE(country) OVER(session_window) AS country,
  LAST_VALUE(device_type) OVER(session_window) AS device_type,
  LAST_VALUE(browser) OVER(session_window) AS browser,
  LAST_VALUE(operating_system) OVER(session_window) AS operating_system,
  FIRST_VALUE(event_date) OVER(session_window) AS session_date,
  FIRST_VALUE(event_timestamp) OVER(session_window) AS session_initiated,
  FIRST_VALUE(page_location) OVER(session_window) AS landing_page,
  LAST_VALUE(page_location) OVER(session_window) AS exit_page,
  FIRST_VALUE(traffic_source) OVER(session_window) AS traffic_source,
  FIRST_VALUE(traffic_campaign) OVER(session_window) AS traffic_campaign,
  FIRST_VALUE(traffic_medium) OVER(session_window) AS traffic_medium,
  FIRST_VALUE(traffic_referrer) OVER(session_window) AS traffic_referrer,
FROM
  ${
      ref({
          schema: dataform.projectConfig.vars.TRF_DATA,
          name: "trf_events_wide_format"
      })
  }
WINDOW
  session_window AS (
  PARTITION BY
    session_id
  ORDER BY
    event_timestamp ROWS BETWEEN UNBOUNDED PRECEDING
    AND UNBOUNDED FOLLOWING )
