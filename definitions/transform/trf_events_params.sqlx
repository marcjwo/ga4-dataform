config {
    type: "incremental",
    schema: dataform.projectConfig.vars.TRF_DATA,
    description: "To unwrap parameters into separate rows (one row per event/parameter pair, or per event for events without parameters)",
    bigquery: {
        partitionBy: "event_date"
    },
    assertions: {
        uniqueKey: ["param_id"]
    },
    tags: ["transform"]
}

SELECT
  -- surrogate key (unique for each event/param pair):
  FARM_FINGERPRINT(CONCAT(event_id,params.key)) AS param_id,
  -- event data:
  event_id,
  event_date,
  event_timestamp,
  client_id,
  user_id,
  event_name,
  -- fields extracted from various struct fields:
  geo.continent AS continent,
  geo.country AS country,
  device.category AS device_type,
  device.web_info.browser AS browser,
  device.operating_system AS operating_system,
  traffic_source.source AS traffic_source,
  traffic_source.name AS traffic_campaign,
  traffic_source.medium AS traffic_medium,
  -- cross-joined parameter key/value pairs:
  params.key AS param_key,
  COALESCE(params.value.string_value, CAST(params.value.int_value AS STRING), CAST(params.value.float_value AS STRING), CAST(params.value.double_value AS STRING) ) AS param_value,
FROM
  ${ref({schema: dataform.projectConfig.vars.STG_DATA, name:"stg_events"})}
LEFT JOIN
  -- unwrap the event parameters: every event/parameter pair is a record
  -- (plus any events without parameters, hence LEFT JOIN)
  UNNEST(event_params) AS params
